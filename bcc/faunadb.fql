CreateIndex({
    name: "bcc-get-tags-by-chat_id-sort_by-tag",
    unique: false,
    serialized: true,
    source: Collection("bcc"),
    terms: [
        {field: ["data", "chat_id"]}
    ],
    values: [
        {field: ["data", "tags"]}
    ]
})


CreateFunction({
    name: "bcc_get_tags",
    body: Query(
        Lambda(
            "chat_id",
            Let(
                {
                    tags: Match(Index("bcc-get-tags-by-chat_id-sort_by-tag"), Var("chat_id"))
                },
                If(
                    Exists(Var("tags")),
                    Select(["data"], Paginate(Var("tags"), {size: 100000})),
                    []
                )
            )
        )
    )
})


CreateFunction({
    name: "bcc_add_tags",
    body: Query(
        Lambda(
            ["chat_id", "tags"],
            Let(
                {
                    x: Match(Index("bcc-get-tags-by-chat_id-sort_by-tag"), Var("chat_id"))
                },
                Select(
                    ["ts"],
                    If(
                        Exists(Var("x")),
                        Update(Select("ref", Get(Var("x"))), {
                            data: {
                                tags: Distinct(Union(Select(["data", "tags"], Get(Var("x"))), Var("tags")))
                            }
                        }),
                        Create(Collection("bcc"), {
                            data: { chat_id: Var("chat_id"), tags: Var("tags") }
                        })
                    )
                )
            )
        )
    )
})
